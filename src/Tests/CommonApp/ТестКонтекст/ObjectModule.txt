//{ основная процедура для юнит-тестирования xUnitFor1C
Перем ЮТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_УстановитьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_ПрочитатьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_ОчиститьКонтекст");
	ВсеТесты.Добавить("ТестДолжен_УстановитьКонтекст_БезИдентификатора");
	ВсеТесты.Добавить("ТестДолжен_ПрочитатьКонтекст_БезИдентификатора");
	ВсеТесты.Добавить("ТестДолжен_ОчиститьКонтекст_БезИдентификатора");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьПолучениеНеустановленногоКонтекста");

	Возврат ВсеТесты;
	
КонецФункции

Функция РазрешенСлучайныйПорядокВыполненияТестов() Экспорт
	Возврат Ложь;
КонецФункции

//}

//{ блок юнит-тестов - сами тесты

Процедура ТестДолжен_УстановитьКонтекст() Экспорт
	Контекст = Новый Структура("Ключ", 10);
	ЮТест.УстановитьКонтекст(Контекст, Идентификатор());
КонецПроцедуры

Процедура ТестДолжен_ПрочитатьКонтекст() Экспорт
	Контекст = ЮТест.ПолучитьКонтекст(Идентификатор());
	ЮТест.ПроверитьРавенство(10, Контекст.Ключ, "передача контекста между тестами не работает");
КонецПроцедуры

Процедура ТестДолжен_ОчиститьКонтекст() Экспорт
	Контекст = Новый Структура("Ключ", 11);
	ЮТест.УстановитьКонтекст(Контекст, Идентификатор());
	ЮТест.УстановитьКонтекст(Неопределено, Идентификатор());
	Контекст = ЮТест.ПолучитьКонтекст(Идентификатор());
	ЮТест.ПроверитьРавенство(Неопределено, Контекст, "Очистка контекста (установка пустого контекста) не работает");
КонецПроцедуры

Процедура ТестДолжен_УстановитьКонтекст_БезИдентификатора() Экспорт
	Контекст = Новый Структура("Ключ", 12);
	ЮТест.УстановитьКонтекст(Контекст);
КонецПроцедуры

Процедура ТестДолжен_ПрочитатьКонтекст_БезИдентификатора() Экспорт
	Контекст = ЮТест.ПолучитьКонтекст();
	ЮТест.ПроверитьРавенство(12, Контекст.Ключ, "передача контекста между тестами не работает");
КонецПроцедуры

Процедура ТестДолжен_ОчиститьКонтекст_БезИдентификатора() Экспорт
	Контекст = Новый Структура("Ключ", 13);
	ЮТест.УстановитьКонтекст(Контекст);
	ЮТест.УстановитьКонтекст(Неопределено);
	Контекст = ЮТест.ПолучитьКонтекст();
	ЮТест.ПроверитьРавенство(Неопределено, Контекст, "Очистка контекста (установка пустого контекста) не работает");
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПолучениеНеустановленногоКонтекста() Экспорт
	
	Идентификатор = "_" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить(Идентификатор);
	
	ТекстОшибки = СтрЗаменить(НСтр("ru = 'Отсутствует запрашиваемый контекст (%1).'"), "%1", Идентификатор);
	
	ЮТест.ПроверитьМетодНеВыполнился(ЮТест, "ПолучитьКонтекст", ПараметрыМетода, ТекстОшибки, НСтр("ru = 'Получили неустановленный контекст.'"));
	
КонецПроцедуры

Функция Идентификатор()
	Возврат "МойКонтекст_097шгр";
КонецФункции

//} 
