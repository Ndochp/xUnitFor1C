&НаКлиенте
Перем ЮТест;  // для тестирования

&НаКлиенте
Перем ВсеТесты;

//{ основная процедура для юнит-тестирования xUnitFor1C
&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮТест = ЮнитТестирование;
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_ПолучитьДанныеТекущегоТеста");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьДанныеТекущегоТеста_Тест2");

	ЮТест.УстановитьКонтекст(Неопределено, ИдентификаторКонтекста());
	
	Возврат ВсеТесты;	
КонецФункции
//}

//{ блок юнит-тестов - сами тесты

&НаКлиенте
Функция РазрешенСлучайныйПорядокВыполненияТестов() Экспорт
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	ОбновитьПовторноИспользуемыеЗначения();
	
	ЮТест.ПроверитьРавенство(2, ВсеТесты.Количество(), "Тестовый сценарий рассчитан только на 2 теста!");
	
	Контекст = ЮТест.ПолучитьКонтекст(ИдентификаторКонтекста());
	Если Не ЗначениеЗаполнено(Контекст) Тогда
		Контекст = Новый Структура("ИмяНабора", ИдентификаторКонтекста());
		ИндексТеста = 0;
	Иначе
		ИндексТеста = 1;
	КонецЕсли;
	Контекст.Вставить("ИмяТеста", ВсеТесты[ИндексТеста]);
	ЮТест.УстановитьКонтекст(Контекст, ИдентификаторКонтекста());
	
	ПроверитьДанныеТекущегоТеста(ЮТест.ЗначенияСостоянияТестов.НеВыполнялся);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
	ПроверитьДанныеТекущегоТеста(ЮТест.ЗначенияСостоянияТестов.Прошел);
КонецПроцедуры

&НаКлиенте
Функция ИдентификаторКонтекста()
	Возврат "Тест_СостояниеТекущегоТеста";
КонецФункции

&НаКлиенте
Процедура ТестДолжен_ПолучитьДанныеТекущегоТеста() Экспорт
	ПроверитьДанныеТекущегоТеста(ЮТест.ЗначенияСостоянияТестов.НеВыполнялся);
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПолучитьДанныеТекущегоТеста_Тест2() Экспорт
	ПроверитьДанныеТекущегоТеста(ЮТест.ЗначенияСостоянияТестов.НеВыполнялся);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДанныеТекущегоТеста(СостояниеВыполнения) Экспорт
	Контекст = ЮТест.ПолучитьКонтекст(ИдентификаторКонтекста());
	
	ДанныеТекущегоТеста = юТест.ДанныеТекущегоТеста();
	ЮТест.ПроверитьРавенство(Контекст.ИмяТеста, ДанныеТекущегоТеста.Имя, "Контекст.ИмяТеста, ДанныеТекущегоТеста.Имя");
	ЮТест.ПроверитьРавенство(СостояниеВыполнения, ДанныеТекущегоТеста.СостояниеТеста, "СостояниеВыполнения, ДанныеТекущегоТеста.СостояниеТеста");
	
	Файл = Новый Файл(ДанныеТекущегоТеста.ПолныйПуть);
	ЮТест.Проверить(Файл.Существует(), "Файл.Существует()");
	ЮТест.ПроверитьРавенство(Контекст.ИмяНабора, Файл.ИмяБезРасширения, "Контекст.ИмяНабора, Файл.ИмяБезРасширения");
КонецПроцедуры

//} конец блока юнит-тестов
